#compdef lb-run

local -a projects
local -a days
local -a common_commands

# at the time of completion, the environment which will be used for execution
# doesn't exist. Therefore, dst-dump and others will not be found by the
# zsh-standard function _command_names. I expand the list by a few hard coded
# commands.

common_commands=(gaudirun.py dst-dump dst-explorer bender db-tags)
days=(Mon Tue Wed Thu Fri Sat Sun today)

# hard coded list of possible projects (I assume non-application projects like
# Phys will not be used ...)
projects=(Brunel Bender DaVinci Moore Urania Erasmus Tesla Gauss LHCbDirac Ganga)
#_describe 'projects' projects

__projects () {
  _describe 'projects' projects
}
__runnablestuff () {
  if [ $CURRENT -eq 4 ] ; then
    _alternative ':commonly used commands:(($common_commands))' \
      'currently available commands::_command_names -e'
  else
    precommands+=($words[1])
    for ((i = $CURRENT; i > 2; i--));
    do
      shift words
      (( CURRENT-- ))
    done
    
    _normal
  fi
}
__list_versions () {
  if [ $CURRENT -eq 3 ] ; then
    local -a versionlist
    # see lb-dev completion for explanation (this is copy&paste here)
    versionlist=($(lb-run -l $@ | sed "s/ .*//" | sort -r -V | uniq | python /afs/cern.ch/user/p/pseyfert/lb-zsh/mysort.py))
    # FIXME: do version sorting in python
    #versionlist=($(lb-run -l $@ | sed "s/ .*//" | python /afs/cern.ch/user/p/pseyfert/lb-zsh/mysort.py))
    _describe -V 'project versions' versionlist
    return
  fi
  __runnablestuff
}

__check_nightlies() {
  for ((i = $NORMARG; i > 2; i--));
  do
    shift words
    (( CURRENT-- ))
  done
  if [ $CURRENT -eq 2 ] ; then
    _alternative '::__projects' ':nightly:((--nightly\:"run nightlies"))'
  elif [ "$words[2]" = "--nightly" ] ; then
    if [ $CURRENT -eq 3 ] ; then
      # slot mandatory
      _alternative ':slots:((lhcb-head lhcb-upgradeTracking))'
    elif [ $CURRENT -eq 4 ] ; then
      # either day or project
      _alternative '::__projects' ':days:(($days))'
    elif [[ $CURRENT -eq 5 && ${days[(r)$words[4]]} == $words[4] ]] ; then
      #http://stackoverflow.com/questions/5203665/zsh-check-if-string-is-in-array
      # if previous was day, then project
      __projects
    else
      # now version (and forwarding)
      slot=$words[3]
      local -a day
      if [[ ${days[(r)$words[4]]} == $words[4] ]] ; then
        day=$words[4]
        shift words
        (( CURRENT-- ))
      else
      fi
      project=$words[4]
      shift words
      shift words
      (( CURRENT-- ))
      (( CURRENT-- ))
      if [ -z "$day" ] ; then
        __list_versions "--nightly" $slot $words[2]
      else
        __list_versions "--nightly" $slot $day $words[2]
      fi
    fi
  else
    # suggest version, or forward further
    __list_versions $words[2]
  fi

}
__listcmt () {
  local -a theenvs
  theenvs=(x86_64-slc6-gcc48-dbg x86_64-slc6-gcc48-opt x86_64-slc6-gcc49-dbg x86_64-slc6-gcc49-do0 x86_64-slc6-gcc49-opt)
  _describe 'cmtconfig' theenvs
}

integer NORMARG
_arguments -n '-l[list versions]' '-c[specify platform]:CMTCONFIG:__listcmt' '*: :__check_nightlies'

