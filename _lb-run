#compdef lb-run

local -a projects
local -a common_commands

# at the time of completion, the environment which will be used for execution
# doesn't exist. Therefore, dst-dump and others will not be found by the
# zsh-standard function _command_names. I expand the list by a few hard coded
# commands.

common_commands=(gaudirun.py dst-dump dst-explorer bender db-tags)

# hard coded list of possible projects (I assume non-application projects like
# Phys will not be used ...)
projects=(Brunel Bender DaVinci Moore Urania Erasmus Tesla Gauss)
#_describe 'projects' projects

__lb_commands () {
  _describe 'commonly used commands' common_commands
}

_arguments '-l[list versions]' '-c::CMTCONFIG:->listcmt' '1:project:($projects)' '2:version:->listversions' '3:command:->runnablestuff' '4:continue:->thenext'

case $state in
  (runnablestuff)
    #_describe 'commonly used commands' common_commands
    _alternative '::__lb_commands' \
      'currently available commands::_command_names -e'
    ;;
  (listversions)
    local -a versionlist
    # see lb-dev completion for explanation (this is copy&paste here)
    versionlist=($(lb-dev -l $line[1] | sed "s/ .*//" | sort -r -V | uniq))
    _describe -V 'project versions' versionlist
    ;;
  (listcmt)
    local -a theenvs
    theenvs=(x86_64-slc6-gcc48-dbg x86_64-slc6-gcc48-opt x86_64-slc6-gcc49-dbg x86_64-slc6-gcc49-do0 x86_64-slc6-gcc49-opt)
    _describe 'cmtconfig' theenvs
    ;;
  (thenext)
    # shift away "lb-run", Project, and version
    # inspired by /usr/share/zsh/functions/Completion/Zsh/_precommand

    # should be 5 in the most simple case, e.g.
    #
    # lb-run DaVinci v40r2 python <TAB>
    # > CURRENT is  5
    # echo " CURRENT is " $CURRENT
    # 
    # and for _normal one needs then three shifts and three (( CURRENT -- ))
    #
    # using a for loop should help when using nightlies or other constellations
    # with increased numbers of arguments

    precommands+=($words[1])
    for ((i = $CURRENT; i > 2; i--));
    do
      shift words
      (( CURRENT-- ))
    done
    
    _normal
    ;;
esac

