#compdef lb-dev

local -a projects
local -a days
days=(Mon Tue Wed Thu Fri Sat Sun today)

# hard coded list of possible projects (I assume non-application projects like
# Phys will not be used ...)
projects=(Brunel Bender DaVinci Moore Urania Erasmus Tesla Gauss)
#_describe 'projects' projects

__projects () {
  compadd "$@" -S '/' ${projects}
}
__list_versions_dev () {
  local -a versionlist
  versionlist=($(lb-run -l $@ | sed "s/ .*//" | sort -r -V | uniq | python /afs/cern.ch/user/p/pseyfert/lb-zsh/mysort.py))
  _describe -V 'project versions' versionlist
}

__check_nightlies_dev() {
  for ((i = $NORMARG; i > 2; i--));
  do
    shift words
    (( CURRENT-- ))
  done
  if [ $CURRENT -eq 2 ] ; then
    if compset -P 1 '*/'; then
      project=${(S)IPREFIX#/}
      __list_versions_dev $project
    else
      _alternative 'projects:Projects:__projects' 'nightlyswitch:nightly:((--nightly\:"run nightlies"))'
    fi
  elif [ "$words[2]" = "--nightly" ] ; then
    if [ $CURRENT -eq 3 ] ; then
      # slot mandatory
      _alternative ':slots:((lhcb-head lhcb-upgradeTracking))'
    elif [ $CURRENT -eq 4 ] ; then
      # either day or project
      if compset -P 1 '*/'; then
        project=${(S)IPREFIX#/}
        slot=$words[3]
        __list_versions_dev "--nightly" $slot $project
      else
        _alternative 'projects:Projects:__projects' 'days:days:(($days))'
      fi
    elif [[ $CURRENT -eq 5 && ${days[(r)$words[4]]} == $words[4] ]] ; then
      #http://stackoverflow.com/questions/5203665/zsh-check-if-string-is-in-array
      # if previous was day, then project

      if compset -P 1 '*/'; then
        project=${(S)IPREFIX#/}
        slot=$words[3]
        local -a day
        day=$words[4]
        __list_versions_dev "--nightly" $slot $day $project
      else
        _alternative ':Projects:__projects'
      fi
    fi
  fi
  # no idea

}
__listcmt () {
  local -a theenvs
  theenvs=(x86_64-slc6-gcc62-dbg x86_64-slc6-gcc62-opt x86_64-slc6-gcc48-dbg x86_64-slc6-gcc48-opt x86_64-slc6-gcc49-dbg x86_64-slc6-gcc49-do0 x86_64-slc6-gcc49-opt)
  _describe 'cmtconfig' theenvs
}

integer NORMARG
_arguments -n '-l[list versions]' '-L[list platforms]' '--name=[name for the local directory]:' '-c[specify platform]:CMTCONFIG:__listcmt' '(- 1)*: :__check_nightlies_dev' '(--debug --quiet --verbose)--debug[print debug messages]' '(--debug --quiet --verbose)--quiet[print only warning messages (default)]' '(--debug --quiet --verbose)--verbose[print more information]'

